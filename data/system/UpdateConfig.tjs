// UpdateConfig.tjs - 用于继承 Config.tjs 的脚本
// Copyright (C)2001-2009, W.Dee and contributors  改变和分发是自由的

// Config.~new 文件是新的 Config.tjs 文件。
// 当 Config.tjs 文件中的版本信息与 KAG 的版本信息不一致时，
// 将调用此脚本文件。

if(Storages.isExistentStorage("Config.~new"))
{
    // 存在 Config.~new 文件

    // 读取 Config.tjs
    var settings = %[]; // 存储设置信息的字典数组
    var oldconfig = [].load("Config.tjs"); // 读取 Config.tjs
    var pattern = /^;\s*(.*?)\s*=(.*)$/; // 用于识别设置行的正则表达式模式
    var pattern_adds = /^\/\/\[start-(.*?)-additionals\]$/;
    for(var i = 0; i < oldconfig.count; i++)
    {
        var matched = pattern.match(oldconfig[i]);
        if(matched.count) settings[matched[1]] = matched[2];
        matched = pattern_adds.match(oldconfig[i]);
        if(matched.count)
        {
            var end_mark = "//[end-" + matched[1] + "-additionals]";
            i++;
            var content = '';
            while(oldconfig[i] != end_mark && i < oldconfig.count)
            {
                content += oldconfig[i] + "\n";
                i++;
            }
            settings["additional" + matched[1]] = content;
        }
    }

    // 写入版本信息
    settings['global.config_version'] = " \"" + kagVersion + "\"; // 请不要删除这一行";

    // 读取 Config.~new 并继承设置
    var newconfig = [].load("Config.~new"); // 读取 Config.~new
    var lines = '';
    for(var i = 0; i < newconfig.count; i++)
    {
        var matched = pattern.match(newconfig[i]);
        var matched_adds = pattern_adds.match(newconfig[i]);
        if(matched.count)
        {
            var setting_name = matched[1];
            var setting = settings[setting_name];
            if(setting !== void)
                lines += ";" + setting_name + " =" + setting + "\n";
            else
                lines += newconfig[i] + "\n";
        }
        else if(matched_adds.count)
        {
            var setting_name = "additional" + matched_adds[1];
            var end_mark = "//[end-" + matched_adds[1] + "-additionals]";
            var setting = settings[setting_name];
            if(setting !== void)
            {
                lines += "//[start-" + matched_adds[1] + "-additionals]\n" +
                    setting + end_mark + "\n";
                i++;
                while(newconfig[i] != end_mark && i < newconfig.count) i++;
            }
            else
            {
                lines += newconfig[i] + "\n";
            }
        }
        else
        {
            lines += newconfig[i] + "\n";
        }
    }

    // 显示 Pad
    global.configPad = new Pad();
    global.configPad.text = lines;
    global.configPad.color = 0;
    global.configPad.title = "Config.tjs";
    global.configPad.fileName = Storages.getLocalName(Storages.getPlacedPath("Config.tjs"));
    global.configPad.visible = true;

    // 通知
    System.inform("已从旧的 Config.tjs 继承设置信息。\n"
        "如果“Config.tjs”窗口中显示的内容没有问题，请在该窗口中\n"
        "右键点击并选择“保存”以保存 Config.tjs。\n"
        "可以删除 Config.~new 文件。");

    // 重新加载新的 config
    Scripts.exec(lines);
}