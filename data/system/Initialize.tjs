// Initialize.tjs - 游戏初始化
// Copyright (C)2001-2009, W.Dee and contributors  改变和分发是自由的

// 系统版本
var kagVersion = "3.32 stable rev. 2";

/*
	Debug.message 的快捷方式
*/

var dm = Debug.message; // 这样可以用 dm("message"); 在控制台显示 message


/*
	默认的“未捕获异常”处理程序
*/

System.exceptionHandler = function (e)
{
	// 当系统捕获到未被任何地方捕获的异常时，会调用此函数。e 是异常对象。
	if(e instanceof "ConductorException")
	{
		// 如果是 Conductor 抛出的异常
		Debug.logAsError(); // 开始将日志写入文件等操作
		var event_disabled = System.eventDisabled;
		System.eventDisabled = true;
			// 在显示错误原因时，如果发生事件会很麻烦，所以暂时停止事件发生
		System.inform(e.message);
		System.eventDisabled = event_disabled;
			// 将事件发生状态恢复到原来的状态
		return true; // 返回 true 则本体侧不会进行异常处理
	}
	else
	{
		return false; // 返回 false 则进行通常的异常处理
	}
};


/*
	路径设置
	后指定的将优先使用
*/


function useArchiveIfExists(name)
{
	// 如果 name 存在则使用该存档
	var arcname;
	if(Storages.isExistentStorage(arcname = System.exePath + name))
		Storages.addAutoPath(arcname + ">");
}

Storages.addAutoPath(System.exePath + "video/"); // exePath 下的 video/
Storages.addAutoPath("video/"); // video 文件夹
Storages.addAutoPath("others/"); // 其他
Storages.addAutoPath("rule/"); // 规则图片文件夹
Storages.addAutoPath("sound/"); // 音效文件夹
Storages.addAutoPath("bgm/"); // BGM 文件夹
Storages.addAutoPath("fgimage/"); // 前景图片文件夹
Storages.addAutoPath("bgimage/"); // 背景图片文件夹
Storages.addAutoPath("scenario/"); // 剧本文件夹
Storages.addAutoPath("image/"); // 其他图片文件夹
Storages.addAutoPath("system/"); // 系统文件夹

// 补丁存档的搜索和使用
// 如果这些名字的存档与可执行文件在同一位置，则优先使用它们
useArchiveIfExists("video.xp3");
useArchiveIfExists("others.xp3");
useArchiveIfExists("rule.xp3");
useArchiveIfExists("sound.xp3");
useArchiveIfExists("bgm.xp3");
useArchiveIfExists("fgimage.xp3");
useArchiveIfExists("bgimage.xp3");
useArchiveIfExists("scenario.xp3");
useArchiveIfExists("image.xp3");
useArchiveIfExists("system.xp3");

useArchiveIfExists("patch.xp3");

// 额外补丁存档的搜索
for(var i = 2; ; i++)
{
	// 如果存在补丁存档 patch2.xp3, patch3.xp3 ... 则优先加载
	if(Storages.isExistentStorage(System.exePath + "patch" + i + ".xp3"))
		Storages.addAutoPath(System.exePath + "patch" + i + ".xp3>");
	else
		break;
}

delete useArchiveIfExists; // useArchiveIfExists 用完后删除

/*
	系统版本
*/
Debug.notice("OS : " + System.osName + " (" + System.platformName + ")");
Debug.notice("KAG : " + kagVersion);
Debug.notice("Kirikiri : " + System.versionString);

/*
	( 调试 ) 时间测量
*/

var parseStartTick = System.getTickCount();


/*
	脚本加载包装器
*/

function KAGLoadScript(name)
{
	var start = System.getTickCount();
	Scripts.execStorage(name);
	dm(name + " 已加载(" + (System.getTickCount() - start) + "ms)");
}

var loaded_scripts = %[];

function KAGLoadScriptOnce(name)
{
	// 加载指定的脚本，但只加载一次
	if(global.loaded_scripts[name] === true) return; // 已经加载过
	global.KAGLoadScript(name);
	global.loaded_scripts[name] = true;
}

/*
	加载 Config.tjs
*/
if(Storages.isExistentStorage("Config.tjs"))
{
	KAGLoadScript("Config.tjs");
}
else if(Storages.isExistentStorage("Config.~new"))
{
	System.inform("Config.tjs 文件未找到。\n请将 system 文件夹中的 "
		"Config.~new 文件重命名为 Config.tjs。");
	System.exit();
}
else
{
	throw new Exception("Config.tjs 文件未找到");
}

/*
	Config.tjs 版本检查
*/

if(typeof global.config_version == "undefined" || config_version != kagVersion)
{
	KAGLoadScript("UpdateConfig.tjs");
}

/*
	双重启动检查
*/

// 使用可执行文件路径作为键进行锁定
if(!System.createAppLock(System.exePath.replace(/[^A-Za-z]/g, '_')))
{
	// 已经启动
	System.inform(System.title + "已经在运行");
	System.exit();
}


/*
	定义按需加载
*/


property askYesNo { getter() { KAGLoadScript("YesNoDialog.tjs"); return global.askYesNo; } }
property CheckBoxLayer { getter() { KAGLoadScript("CheckBoxLayer.tjs"); return global.CheckBoxLayer; } }
property ButtonLayer { getter() { KAGLoadScript("ButtonLayer.tjs"); return global.ButtonLayer; } }
property EditLayer { getter() { KAGLoadScript("EditLayer.tjs"); return global.EditLayer; } }
property KAGPlugin { getter() { KAGLoadScript("Plugin.tjs"); return global.KAGPlugin; } }

/*
	加载各系统
*/
dm("正在加载 KAG 系统脚本...");

KAGLoadScript("Utils.tjs");
KAGLoadScript("KAGLayer.tjs");
KAGLoadScript("HistoryLayer.tjs");
KAGLoadScript("BGM.tjs");
KAGLoadScript("SE.tjs");
KAGLoadScript("Movie.tjs");
KAGLoadScript("Conductor.tjs");
KAGLoadScript("AnimationLayer.tjs");
KAGLoadScript("GraphicLayer.tjs");
KAGLoadScript("MessageLayer.tjs");
KAGLoadScript("Menus.tjs");
KAGLoadScript("DefaultMover.tjs");
KAGLoadScript("MainWindow.tjs");
if(Storages.isExistentStorage("Override.tjs"))
	KAGLoadScript("Override.tjs");
if(Storages.isExistentStorage(System.exePath + "Override2.tjs"))
	KAGLoadScript(System.exePath + "Override2.tjs");


/*
	( 调试 ) 时间测量
*/
dm("加载脚本花费了 " + (System.getTickCount() - parseStartTick) + "ms");
parseStartTick = System.getTickCount();

/*
	( 调试 ) VM 代码转储
*/

// Scripts.dump();

/*
	( 调试 ) 时间测量
*/

parseStartTick = System.getTickCount();


/*
	创建 KAG 主窗口
	如果全局成员 kag 不存在，则创建 KAGWindow 类的对象并赋值给它
*/

global.kag = new KAGWindow() if typeof global.kag == "undefined";




/*
	为了便于从全局访问，创建一些变量的别名
*/

var f = kag.flags;   // 用户变量 (标志)
var sf = kag.sflags; // 系统变量 (系统)
var tf = kag.tflags; // 临时变量 (临时标志)

property mp
{
	getter { return kag.conductor.macroParams; }
}

/*
	( 调试 ) 时间测量
*/
dm("KAGMainWindow 的构造函数花费了 " + (System.getTickCount() - parseStartTick) + "ms");
delete parseStartTick;


/*
	如果存在 AfterInit.tjs 则执行
*/
if(Storages.isExistentStorage("AfterInit.tjs"))
	KAGLoadScript("AfterInit.tjs");
if(Storages.isExistentStorage(System.exePath + "AfterInit2.tjs"))
	KAGLoadScript(System.exePath + "AfterInit2.tjs");

/*
	如果命令行参数中指定了 -ovr，则将该参数作为 TJS 表达式执行
*/
{
	var ovr = System.getArgument('-ovr');
	if(ovr !== void && ovr != 'yes') Scripts.eval(ovr);
}

/*
	启动时的脚本
*/

kag.process("start.ks");
