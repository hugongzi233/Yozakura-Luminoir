// YesNoDialog.tjs - 是/否选择对话框
// Copyright (C)2001-2009, W.Dee and contributors  改变和分发是自由的

class YesNoDialogWindow extends Window
{
    var yesButton; // [是] 按钮
    var noButton; // [否] 按钮

    var result = false; // no:false yes:true

    function YesNoDialogWindow(message, cap)
    {
        super.Window();

        // 这个窗口使用最基本的drawer
        // drawDevice.preferredDrawer = global.Window.PassThroughDrawDevice.dtDrawDib;

        // 从主窗口获取 cursor**** 的信息
        if(global.Window.mainWindow !== null &&
            typeof global.Window.mainWindow.cursorDefault != "undefined")
            this.cursorDefault = global.Window.mainWindow.cursorDefault;
        if(global.Window.mainWindow !== null &&
            typeof global.Window.mainWindow.cursorPointed != "undefined")
            this.cursorPointed = global.Window.mainWindow.cursorPointed;

        // 外观调整
        borderStyle = bsDialog;
        innerSunken = false;
        caption = cap;
        showScrollBars = false;

        // 创建主层
        add(new Layer(this, null));

        // 设置主层的鼠标光标
        if(typeof this.cursorDefault !== "undefined")
            primaryLayer.cursor = cursorDefault;

        // 确定大小
        var tw = primaryLayer.font.getTextWidth(message);
        var th = primaryLayer.font.getTextHeight(message);

        var w = tw + 40;
        if(w<200) w = 200;
        var h = th*2 + 60;

        // 设置缩放比例
        if (kag.fullScreen) {
            if (kag.innerWidth / kag.scWidth
                < kag.innerHeight / kag.scHeight)
                setZoom(kag.innerWidth, kag.scWidth);
            else
                setZoom(kag.innerHeight, kag.scHeight);
        } else {
          setZoom(kag.zoomNumer, kag.zoomDenom);
        }
        // 确定大小
        setInnerSize(w * zoomNumer / zoomDenom,
             h * zoomNumer / zoomDenom);

        // 设置主层的大小
        primaryLayer.width = w;
        primaryLayer.height = h;
        primaryLayer.colorRect(0, 0, w, h, clBtnFace, 255);

        // 调整窗口位置
        if(global.Window.mainWindow !== null && global.Window.mainWindow isvalid)
        {
            var win = global.Window.mainWindow;
            var l, t;
            l = ((win.width - width)>>1) + win.left;
            t = ((win.height - height)>>1) + win.top;
            if(l < 0) l = 0;
            if(t < 0) t = 0;
            if(l + width > System.screenWidth) l = System.screenWidth - width;
            if(t + height > System.screenHeight) t = System.screenHeight - height;
            setPos(l, t);
        }
        else
        {
            setPos((System.screenWidth - width)>>1, (System.screenHeight - height)>>1);
        }

        // 绘制消息
        primaryLayer.drawText((w - tw)>>1, 14, message, clBtnText);

        // 是按钮
        add(yesButton = new ButtonLayer(this, primaryLayer));
        yesButton.caption = "是的";
        yesButton.captionColor = clBtnText;
        yesButton.width = 70;
        yesButton.height = 25;
        yesButton.top = th + 35;
        yesButton.left = (w - (70*2 + 10)>>1);
        yesButton.visible = true;

        // 否按钮
        add(noButton = new ButtonLayer(this, primaryLayer));
        noButton.caption = "取消";
        noButton.captionColor = clBtnText;
        noButton.width = 70;
        noButton.height = 25;
        noButton.top = th + 35;
        noButton.left = ((w - (70*2 + 10))>>1) + 70 + 10;
        noButton.visible = true;

    }

    function finalize()
    {
        super.finalize(...);
    }

    function action(ev)
    {
        // action
        if(ev.type == "onClick")
        {
            if(ev.target == yesButton)
            {
                result = true;
                close();
            }
            else if(ev.target == noButton)
            {
                result = false;
                close();
            }
        }
        else if(ev.type == "onKeyDown" && ev.target === this)
        {
            // 处理手柄输入
            switch(ev.key)
            {
            case VK_PADLEFT:
                yesButton.focus();
                break;
            case VK_PADRIGHT:
                noButton.focus();
                break;
            case VK_PAD1:
                if(focusedLayer == yesButton)
                {
                    result = true;
                    close();
                }
                else if(focusedLayer == noButton)
                {
                    result = false;
                    close();
                }
                break;
            case VK_PAD2:
                result = false;
                close();
                break;
            }
        }
    }

    function onKeyDown(key, shift)
    {
        super.onKeyDown(...);
        if(key == VK_ESCAPE)
        {
            // 按下 ESC 键
            // 作为“否”处理
            result = false;
            close();
        }
    }
}

// 确认是或否的函数
function askYesNo(message, caption = "确认")
{
    var win = new YesNoDialogWindow(message, caption);
    win.showModal();
    var res = win.result;
    invalidate win;
    return res;
}