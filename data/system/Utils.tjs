// Utils.tjs - 实用函数
// Copyright (C)2001-2009, W.Dee and contributors  改变和分发是自由的

property random
{
    getter { return Math.random(); }
}

function intrandom(min = 0, max = 0) 
{
    // 返回 min 以上 max 以下的整数随机数
    // 如果只有一个参数，则返回 0 到该数的整数
    if(min>max) { min <-> max; }
    return int(Math.random() * (max-min+1)) + min;
}

function str2num(str)
{
    // 字符串->数字 ( 全角支持 )
    var res;
    var i;
    for(i=0; i<str.length; i++)
    {
        var ch = str[i];
        switch(ch)
        {
        case "０": res+="0"; break;
        case "１": res+="1"; break;
        case "２": res+="2"; break;
        case "３": res+="3"; break;
        case "４": res+="4"; break;
        case "５": res+="5"; break;
        case "６": res+="6"; break;
        case "７": res+="7"; break;
        case "８": res+="8"; break;
        case "９": res+="9"; break;
        case "ｅ": res+="e"; break;
        case "Ｅ": res+="E"; break;
        case "。": res+="."; break;
        case "．": res+="."; break;
        case "−": res+="-"; break;
        case "ー": res+="-"; break;
        default: res+=ch; break;
        }
    }
    return +res;
}


function han2zen(str)
{
    // 半角->全角 ( 仅限英数字 )
    var res;
    var i;
    for(i=0;i<str.length;i++)
    {
        var num=#str[i];
        if(num>=0x0020 && num<=0x7e)
            res+=$(0xff00+num-0x20); // UNICODE
        else res+=str[i];
    }
    return res;
}


function kansuuji(
    n,
    digits = "〇一二三四五六七八九",
    small_units = "　十百千",
    large_units = "　万億兆京",
    zero_expression = "ゼロ",
    minus_expression = "マイナス"
    )
{
    // 将 n 转换为一般的汉字数字表示
    // TJS 的整数型可以表示到 922京，因此不需要处理京以上的位数

    n = int n;
    if(n == 0) return zero_expression;
    var out = ""; // 输出字符串
    if(n < 0) n = -n, out = minus_expression;
    n = string n; // 转换为字符串

    var n_len = n.length;
    var n_pos = n_len - 1;
    var nonzero = false;

    for(var i = 0; i < n_len; i ++, n_pos --)
    {
        var small_unit = n_pos & 3;
        var digit = +n[i];
        switch(small_unit)
        {
        case 0: // 个位
            if(digit != 0) out += digits[digit], nonzero = true;
            if(nonzero && n_pos) out += large_units[n_pos >> 2];
            nonzero = false;
            break;
        case 1: // 十位
        case 2: // 百位
        case 3: // 千位
            if(digit != 0)
            {
                /* 千位在万以上时习惯性地说一千。
                   另外，不说一百或一十。 */
                if(digit != 1 || (small_unit == 3 && n_pos > 4))
                    out += digits[digit] + small_units[small_unit];
                else
                    out += small_units[small_unit];
                nonzero = true;
            }
            break;
        }
    }

    return out;
}

function kansuuji_simple(
    n,
    digits = "〇一二三四五六七八九",
    point = "・",
    minus = "マイナス")
{
    // 将 n 转换为汉字数字表示，但不添加位数单位

    n = string n;
    var n_len = n.length;
    var out = "";
    for(var i = 0; i < n_len; i++)
    {
        var digit = n[i];
        if(digit == ".")
            out += point;
        else if(digit == "-")
            out += minus;
        else if(digit >= '0' && digit <= '9')
            out += digits[+digit];
        else
            out += digit;
    }
    return out;
}

function number_format(n)
{
    // 将 n 转换为每三位用逗号分隔的数字表示
    n = string n;
    var n_len = n.length;
    var n_digits = 0;

    // 计算数字的位数
    for(var i = 0; i < n_len; i++)
    {
        var digit = n[i];
        if(digit >= '0' && digit <= '9') n_digits ++;
        else if(digit == '.' || digit == 'e') break;
    }

    var out = "";

    // 插入逗号
    for(var i = 0; i < n_len; i++)
    {
        var digit = n[i];
        if(digit >= '0' && digit <= '9')
        {
            n_digits --;
            out += digit;
            if(n_digits > 0 && n_digits % 3 == 0)
                out += ",";
        }
        else
        {
            out += digit;
        }
    }

    return out;
}